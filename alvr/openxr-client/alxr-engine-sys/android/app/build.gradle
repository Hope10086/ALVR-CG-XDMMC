plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 33
    ndkVersion '25.2.9519653'

    defaultConfig {
        //applicationId "com.alvr.alxr_client"
        minSdk 28 // to enable official low-latency api mode, minsdk level must be >= 30
        targetSdk 29
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++20 -fexceptions -frtti"
                arguments "-DPython_ROOT_DIR='C:/Users/korcan/AppData/Local/Programs/Python/Python310'", "-DANDROID_ARM_NEON=TRUE",
                        "-DBUILD_API_LAYERS=OFF", "-DBUILD_TESTS=OFF", "-DBUILD_CONFORMANCE_TESTS=OFF", '-DBUILD_ALL_EXTENSIONS=ON'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            path file('../../cpp/ALVR-OpenXR-Engine/CMakeLists.txt')
            version '3.22.1'
        }
    }

    flavorDimensions 'headset'
    productFlavors {
        Generic {
            dimension 'headset'
            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64', 'x86'
            }
            externalNativeBuild.cmake.arguments += [/*"-DANDROID_STL=c++_shared",*/ "-DBUILD_LOADER=ON", "-DUSE_OCULUS_MOBILE_LOADER=OFF"]
        }
        OculusMobileOXR {
            dimension 'headset'
            //applicationIdSuffix '.oculus'
            ndk {
                abiFilters 'arm64-v8a'
            }
            externalNativeBuild.cmake.arguments += ["-DBUILD_LOADER=OFF", "-DUSE_OCULUS_MOBILE_LOADER=ON"]
        }
        PicoMobileOXR {
            dimension 'headset'
            ndk {
                abiFilters 'arm64-v8a'
            }
            externalNativeBuild.cmake.arguments += [
                    "-DANDROID_TOOLCHAIN=clang", "-DANDROID_STL=c++_static",
                    "-DBUILD_LOADER=OFF", "-DUSE_PICO_MOBILE_LOADER=ON"
            ]
        }
        PicoMobileOXR_V4 {
            dimension 'headset'
            ndk {
                abiFilters 'arm64-v8a'
            }
            externalNativeBuild.cmake.arguments += [
                    "-DANDROID_TOOLCHAIN=clang", "-DANDROID_STL=c++_static",
                    "-DBUILD_LOADER=OFF", "-DUSE_PICO_MOBILE_V4_LOADER=ON"
            ]
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    //implementation 'org.khronos.openxr:openxr_loader_for_android:1.0.20'
    //implementation files('libs/openxr_loader_for_android-1.0.22.aar')
}